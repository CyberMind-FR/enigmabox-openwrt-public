
bundle agent app_security
{
  vars:
      "site" data => readjson("$(g.site)", 64000);

  classes:
      "webinterface_password" expression => strcmp("$(site[if_webinterface_password])", "true");

  files:
      "/etc/lighttpd/auth/."
      create => "true";

    webinterface_password::
      "/etc/lighttpd/auth/webinterface.conf"
      create => "true",
      template_method => "mustache",
      template_data => readjson("$(g.site)", 64000),
      edit_template => "$(this.promise_dirname)/templates/webinterface.conf.mustache",
      edit_defaults => no_backup,
      classes => if_repaired("restart_lighttpd");

      "/etc/lighttpd/auth/webinterface.htpasswd"
      create => "true",
      template_method => "mustache",
      template_data => readjson("$(g.site)", 64000),
      edit_template => "$(this.promise_dirname)/templates/webinterface.htpasswd.mustache",
      edit_defaults => no_backup,
      perms => wwwdata,
      classes => if_repaired("restart_lighttpd");

    !webinterface_password::
      "/etc/lighttpd/auth/webinterface.conf"
      delete => tidy,
      classes => if_repaired("restart_lighttpd");

  commands:
    restart_lighttpd::
      "/etc/init.d/lighttpd restart";

  reports:
      "checking security: done";
}

body perms wwwdata
{
      mode => "600";
      owners => { "nobody" };
      groups => { "nogroup" };
}

