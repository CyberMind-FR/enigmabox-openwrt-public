
bundle agent app_security
{
  vars:
      "site" data => readjson("$(g.site)", 64000);

  classes:
      "webinterface_password" expression => strcmp("$(site[if_webinterface_password])", "true");

  files:
      "/etc/nginx/auth/."
      create => "true";

    webinterface_password::
      "/etc/nginx/auth/webinterface.conf"
      create => "true",
      template_method => "mustache",
      template_data => readjson("$(g.site)", 64000),
      edit_template => "$(this.promise_dirname)/templates/webinterface.conf",
      classes => if_repaired("restart_nginx");

      "/etc/nginx/auth/webinterface.htpasswd"
      create => "true",
      template_method => "mustache",
      template_data => readjson("$(g.site)", 64000),
      edit_template => "$(this.promise_dirname)/templates/webinterface.htpasswd.mustache",
      perms => wwwdata,
      classes => if_repaired("restart_nginx");

    !webinterface_password::
      "/etc/nginx/auth/webinterface.conf"
      delete => tidy,
      classes => if_repaired("restart_nginx");

  commands:
    restart_nginx::
      "/etc/init.d/nginx restart";

  reports:
      "app_security";
}

body perms wwwdata
{
      mode => "600";
      owners => { "nobody" };
      groups => { "nogroup" };
}

