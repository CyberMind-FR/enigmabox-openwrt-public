
bundle agent app_email
{
  vars:
      "pwd[uucp]" string => "uucp:x:10:10:uucp:/var/spool/uucp:/bin/sh";
      "pwd[mail]" string => "mail:x:8:8:mail:/var/mail:/bin/sh";
      "pwd[exim]" string => "exim:x:110:110:exim:/var/mail:/bin/sh";
      "pwd[dovecot]" string => "dovecot:x:106:106:Dovecot mail server:/usr/lib/dovecot:/bin/false";
      "pwd[dovenull]" string => "dovenull:x:107:107:Dovecot login user:/nonexistent:/bin/false";
      "pwd[vmail]" string => "vmail:x:5000:5000:virtual mail user:/box/vmail:/bin/sh";
      "grp[uucp]" string => "uucp:x:10:";
      "grp[mail]" string => "mail:x:8:";
      "grp[exim]" string => "exim:x:110:";
      "grp[dovecot]" string => "dovecot:x:106:";
      "grp[vmail]" string => "vmail:x:5000:";

  files:
      "/usr/exim/configure"
      create => "true",
      template_method => "mustache",
      template_data => readjson("$(g.site)", 64000),
      edit_template => "$(this.promise_dirname)/templates/exim4.conf.mustache",
      classes => if_repaired("restart_exim");

      "/etc/dovecot/dovecot.conf"
      create => "true",
      edit_template => "$(this.promise_dirname)/templates/dovecot.conf",
      classes => if_repaired("restart_dovecot");

      "/box/vmail/."
      create => "true",
      perms => vmail,

      "/etc/dovecot/users.conf"
      create => "true",
      perms => file,
      template_method => "mustache",
      template_data => readjson("$(g.site)", 64000),
      edit_template => "$(this.promise_dirname)/templates/users.conf.mustache",
      classes => if_repaired("restart_dovecot");

      "/etc/php.ini"
      create => "true",
      edit_template => "$(this.promise_dirname)/templates/php.ini",
      classes => if_repaired("restart_php");

      "/etc/passwd"
      edit_line => append_users_starting("app_email.pwd");

      "/etc/group"
      edit_line => append_groups_starting("app_email.grp");

  commands:
    restart_exim::
      "/etc/init.d/exim restart";

    restart_dovecot::
      "/etc/init.d/dovecot restart";

    restart_php::
      "/etc/init.d/php5-fastcgi restart";

  reports:
      "app_email";
}

body perms vmail
{
      mode => "755";
      owners => { "vmail" };
      groups => { "vmail" };
}

